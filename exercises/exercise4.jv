pipeline Exercise4_Trees {

    DataDownloader
    -> ZipHandler 
    -> DataFilePicker
    -> TextFileInterpreter;


    TextFileInterpreter
        -> CSVHandler
        -> ColumnHeaderWriter
        -> TemperatureWriter
        -> DataRangeSelector
        -> TableInterpreter
        -> TemperatureTransformer
        -> BatteryTemperatureTransformer
        -> DataLoader;

    
    block DataDownloader oftype HttpExtractor {
        url: "https://www.mowesta.com/data/measure/mowesta-dataset-20221107.zip";
    }
     block TableInterpreter oftype TableInterpreter {
        header: true;
        columns: [
            "id" oftype PositiveIntegersValue,
            "producer" oftype text,
            "model" oftype text,
            "month" oftype integer,
            "temperature" oftype decimal,
            "battery_temperature" oftype decimal
        ];
    }

    block ZipHandler oftype ArchiveInterpreter { archiveType: "zip";}

    block CSVHandler oftype CSVInterpreter {
       delimiter: ";";
    }

    block DataFilePicker oftype FilePicker { path: "/data.csv";}

    block TextFileInterpreter oftype TextFileInterpreter {}
    
    block ColumnHeaderWriter oftype CellWriter {
        at: range A1:E1;
        write: ["id", "producer", "model", "month", "temperature"];
    }

    block BatteryTemperatureTransformer oftype TableTransformer {
        inputColumns: ["battery_temperature"];
        outputColumn: "battery_temperature";
        use: ConvertTemperature;
    }

    block TemperatureWriter oftype CellWriter {
        at: cell J1;
        write: ["battery_temperature"];
    }

    block DataRangeSelector oftype CellRangeSelector {
        select: range A1:J*;
    }

    block DataLoader oftype SQLiteLoader {
        table: "temperatures";
        file: "./temperatures.sqlite";
    }

    block TemperatureTransformer oftype TableTransformer {
        inputColumns: ["temperature"];
        outputColumn: "temperature";
        use: ConvertTemperature;
    }

    valuetype PositiveIntegersValue oftype integer {
        constraints: [ PositiveIntValue ];
    }
    
    constraint PositiveIntValue oftype RangeConstraint {
        lowerBound: 1;
    }

     transform ConvertTemperature {
        from Celsius oftype decimal;
        to Fahrenheit oftype decimal;
        Fahrenheit : (Celsius * 9/5) + 32;
    }
}
